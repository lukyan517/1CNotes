# Универсальные процедуры и функции 01

## 1. Получаем таблицу значений на основании колонок запроса

Используеться если нужно получить таблицу значений на основании выходных колонок запроса, но без выгрузки таблицы из результат запроса.

```bsl

Функция ТаблицаЗначенийИзКолонокЗапроса(РезультатЗапроса)

	ТаблицаЗначений = Новый ТаблицаЗначений;
	
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		ТаблицаЗначений.Колонки.Добавить(Колонка.Имя, Новый ОписаниеТипов(Колонка.ТипЗначения,, "Null"));
	КонецЦикла;

	Возврат ТаблицаЗначений;

КонецФункции

```

## 2. Оставить только нужные символы в строке

```bsl

Функция ОставитьТолькоНужныеСимволы(Знач ИсходнаяСтрока = "", Кириллица = Ложь, Латиница = Ложь, Числа = Ложь, Знач НужныеСимволы = "", ПервыйСимволТолькоБуква = Ложь)

	Результат = "";
	
	СимволыКириллицы = "";
	Для НомерСимвола = 1040 По 1103 Цикл 
		СимволыКириллицы = СимволыКириллицы + Символ(НомерСимвола);
	КонецЦикла;
	
	СимволыЛатиницы = "";
	Для НомерСимвола = 65 По 90 Цикл 
		СимволыЛатиницы = СимволыЛатиницы + Символ(НомерСимвола);
	КонецЦикла;
	
	Для НомерСимвола = 97 По 122 Цикл 
		СимволыЛатиницы = СимволыЛатиницы + Символ(НомерСимвола);
	КонецЦикла;
	
	НужныеСимволы = НужныеСимволы + ?(Кириллица, 	СимволыКириллицы, "");
	НужныеСимволы = НужныеСимволы + ?(Латиница, 	СимволыЛатиницы, "");
	НужныеСимволы = НужныеСимволы + ?(Числа, 		"0123456789", "");
	
	Для НомерСимвола = 1 По СтрДлина(ИсходнаяСтрока) Цикл
		
		ТекущийСимвол = Сред(ИсходнаяСтрока, НомерСимвола, 1);
		
		Если Найти(НужныеСимволы, ТекущийСимвол) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПервыйСимволТолькоБуква
			И Результат = ""
			И Найти(СимволыКириллицы + СимволыЛатиницы, ТекущийСимвол) = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Результат = Результат + ТекущийСимвол;

	КонецЦикла;
	
	Возврат Результат;

КонецФункции

```
