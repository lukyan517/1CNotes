# Конструкции: попытка, транзакция

## Выполнение операции на сервере в попытке, с записью в журнал регистрации диагностических сообщеий

```bsl

&НаСервере
Процедура ВыполнитьОперациюНаСервере()
	Попытка
		// код, приводящий к вызову исключения
	Исключение
		// Запись события в журнал регистрации для системного администратора.
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операции'"), УровеньЖурналаРегистрации.Ошибка,,,
		ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОперацию()
	Попытка
		ВыполнитьОперациюНаСервере();
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ПоказатьПредупреждение(,НСтр("ru = 'Операция не может быть выполнена по причине:'") + Символы.ПС + ТекстСообщения);
	КонецПопытки;
КонецПроцедуры

```

## Выполнение операций в транзации

```bsl

НачатьТранзакцию();
Попытка
    ДелаемЧтоТо();
    ЗафиксироватьТранзакцию();
Исключение
    Если ТранзакцияАктивна() Тогда
        ОтменитьТранзакцию();
    КонецЕсли;
    ВызватьИсключение;
КонецПопытки;

```
